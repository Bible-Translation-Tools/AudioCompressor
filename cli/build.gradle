plugins {
    id "com.github.johnrengelman.shadow" version "$shadowPluginVer"
}

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'edu.sc.seis.launch4j'

version = '0.2.0'

application {
    mainClassName = "org.bibletranslationtools.audiocompressor.Main"
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "info.picocli:picocli:$picocliVer"
    kapt "info.picocli:picocli-codegen:$picocliVer"

    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVer"
    implementation "com.github.thomasnield:rxkotlinfx:$rxkotlinfxVer"

    implementation project(':common')
}

kapt {
    arguments {
        arg("project", "${project.group}/${project.name}")
    }
}

shadowJar {
    archiveFileName = "${rootProject.name}-${project.name}.jar"
    mergeServiceFiles()
    manifest {
        attributes(
                'Main-Class': "org.bibletranslationtools.audiocompressor.Main"
        )
    }
}

task copyJre(type: Copy) {
    if (project.hasProperty("jrePath")) {
        from jrePath
        into "${buildDir}/launch4j/jre"
    }
}

launch4j {
    outfile = "${rootProject.name}-${project.name}.exe"
    mainClassName = project.mainClassName
    bundledJrePath = "./jre"
    bundledJre64Bit = true
    jdkPreference = 'jdkOnly'
    jar = "${buildDir}/libs/${rootProject.name}-${project.name}.jar"
    productName = rootProject.name
    fileDescription = rootProject.name
}

project.tasks.createExe.dependsOn copyJre
project.tasks.createExe.dependsOn shadowJar
